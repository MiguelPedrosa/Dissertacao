import Loader;
import Runner;
import Globals;
import Evalualtor;

aspectdef Launcher
	input benchmarkFolder = "./benchmarks", outputFolder = "output" end

//  setDebug(true);

  var transformations = getTransformations();
  var benchmarks = getBenchmarksInfo();

  for (var benchmark of benchmarks) {
    var name = benchmark.name;
    loadBenchmark(benchmarkFolder + "/" + name);
    // Run non-modified version and save results
    var baseResult = runBenchmark(name, true);

    // Find all loops candidates and 
    // apply each transformation to every candidates
    var $loops = getUVECandidates();
    for (var fun of transformations) {
      for (var $loop of $loops) {
        fun($loop);
      }
    }

    // Run modified version and save results
    var modResult = runBenchmark(name, true);

    // Store produced code into separate file
    var outName = outputFolder + "/" + name;
    Clava.writeCode(outName);

    // Compare execution results and stop if they differ
    var areResultsEqual = compareExecutionResults(name, baseResult, modResult);
    if (!areResultsEqual) {
      throw new Error("Results of benchmark: " + name + " were different");
    }
  }

  println("ALL BENCHMARKS PASSED");
end
